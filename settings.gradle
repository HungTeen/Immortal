pluginManagement {
    plugins{
        // Apply the plugin. You can find the latest version at https://projects.neoforged.net/neoforged/ModDevGradle
        id 'net.neoforged.moddev' version '2.0.31-beta' apply false
        id 'net.neoforged.gradle.userdev' version '7.0.142' apply false
        id 'net.minecraftforge.gradle' version '6.0.24' apply false
        id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' apply false
        id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply false
        id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    }

    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven {
            name = 'NeoForge'
            url = 'https://maven.neoforged.net/releases'
            content {
                includeGroupAndSubgroups("net.neoforged")
                includeGroup("codechicken")
                includeGroup("net.covers1624")
            }
        }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
            content {
                includeGroupAndSubgroups("net.fabricmc")
                includeGroup("fabric-loom")
            }
        }
        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net'
            content {
                includeGroupAndSubgroups("net.minecraftforge")
            }
        }
        maven {
            name = 'Sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'Immortal'

include('common')
include('neoforge')